""" Section: This Stuff Should Stay at the Top
let $VIMHOME = $HOME . '/.vim/'
let g:mapleader = "\<Space>"
" if 'encoding' is set, 'scriptencoding' must be set after it
set encoding=utf8
scriptencoding utf-8

""" Section: General
set confirm
set mouse=a
" enable mouse mode while in tmux
let &ttymouse = has('mouse_sgr') ? 'sgr' : 'xterm2'
set backspace=indent,eol,start
set hidden
set nrformats-=octal
set ttimeout ttimeoutlen=100
set updatetime=500
set clipboard=unnamed
set autoread " Re-read file if it is changed by an external program
set scrolloff=10
set sessionoptions-=blank sessionoptions-=options sessionoptions+=tabpages sessionoptions-=folds
set showcmd
set display+=lastline
set nofoldenable

function! OverrideVimsDefaultFiletypePlugins()
  " there is an ftplugin that is bundled with vim that
  " sets the textwidth to 78 if it is currently 0. This sets it back to 0
  set textwidth=0

  " Vim's default filetype plugins get run after filetype detection is
  " performed (i.e. ':filetype plugin on'). So in order to override
  " settings from vim's filetype plugins, the FileType autocommands
  " need to be registered after filetype detection.
  " Since this function gets called on the VimEnter event, we're
  " certain that filetype detection has already
  " happened. In this case it gets triggered when the plugin manager
  " finishes loading plugins.
  augroup OverrideFiletypePlugins
    autocmd!
    " Use vim help pages for keywordprg in vim files
    autocmd FileType vim setlocal keywordprg=:tab\ help
    " Set a default omnifunc
    autocmd FileType * if &omnifunc == "" | setlocal omnifunc=syntaxcomplete#Complete | endif
  augroup END
endfunction

augroup Miscellaneous
  autocmd!
  " Extend iskeyword for filetypes that can reference CSS classes
  autocmd FileType
    \ css,scss,javascriptreact,typescriptreact,javascript,typescript,sass,postcss
    \ setlocal iskeyword+=-,?,!
  autocmd FileType vim setlocal iskeyword+=:,#
  autocmd FileType tmux setlocal iskeyword+=-
  autocmd BufEnter *
        \ if &ft ==# 'help' && (&columns * 10) / &lines > 31 | wincmd L | endif
  autocmd FileType sh setlocal keywordprg=man
  autocmd VimEnter * call OverrideVimsDefaultFiletypePlugins()
  autocmd CursorHold * execute printf('silent! 3match WordUnderCursor /\V\<%s\>/', escape(expand('<cword>'), '/\'))
  " After a quickfix command is run, open the quickfix window , if there are results
  autocmd QuickFixCmdPost [^l]* cwindow
  autocmd QuickFixCmdPost l*    lwindow
  " Put focus back in quickfix window after opening an entry
  autocmd FileType qf nnoremap <buffer> <CR> <CR><C-W>p
  " highlight trailing whitespace
  autocmd ColorScheme * highlight ExtraWhitespace ctermbg=red | execute '2match ErrorMsg /\s\+$/'
  " Automatically resize all splits to make them equal when the vim window is
  " resized or a new window is created/closed
  autocmd VimResized,WinNew,WinClosed * wincmd =
  autocmd BufWinEnter * if line2byte(line("$") + 1) > 1000000 | syntax clear | else | :syntax sync fromstart | endif
  autocmd! bufwritepost .vimrc,vimrc nested source $MYVIMRC | execute 'colorscheme ' . trim(execute('colorscheme'))
augroup END

cnoreabbrev <expr> h getcmdtype() == ":" && getcmdline() == 'h' ? 'tab help' : 'h'

" set swapfile directory
let &directory = $VIMHOME . 'swapfile_dir/'
call mkdir(&directory, "p")

" persist undo history to disk
let &undodir = $VIMHOME . 'undo_dir/'
call mkdir(&undodir, "p")
set undofile

" set backup directory
let &backupdir = $VIMHOME . 'backup_dir/'
call mkdir(&backupdir, "p")
set backup

" tab setup
set expandtab
set autoindent smartindent
set smarttab
set shiftround " Round indent to multiple of shiftwidth (applies to < and >)
let s:tab_width = 2
let &tabstop = s:tab_width
let &shiftwidth = s:tab_width
let &softtabstop = s:tab_width

" Display all highlight groups in a new window
command! HighlightTest so $VIMRUNTIME/syntax/hitest.vim

" tabs
nnoremap <silent> <Leader>c <Cmd>tabnew<CR>
nnoremap <silent> <C-h> <Cmd>tabprevious<CR>
nnoremap <silent> <C-l> <Cmd>tabnext<CR>

inoremap jk <Esc>
nnoremap <silent> <Leader>w <Cmd>wa<CR>
nnoremap <Leader>x <Cmd>wqa<CR>

" maximize a window by opening it in a new tab
nnoremap <silent><C-m> <Cmd>tab sp<CR>
vnoremap <silent><C-m> <Cmd>tab sp<CR>
inoremap <silent><C-m> <Cmd>tab sp<CR>

" open new horizontal and vertical panes to the right and bottom respectively
set splitright splitbelow
nnoremap <Leader>\| <Cmd>vsplit<CR>
nnoremap <Leader>- <Cmd>split<CR>
" close a window, quit if last window
" also when closing a tab, go to the previously opened tab
nnoremap <silent> <expr> <leader>q  winnr('$') == 1 ? ':exe "q" \| silent! tabn '.g:lasttab.'<CR>' : ':close<CR>'
" track which tab last opened
if !exists('g:lasttab')
  let g:lasttab = 1
endif
autocmd TabLeave * let g:lasttab = tabpagenr()

""" Section: Autocomplete
" show the completion menu even if there is only one suggestion
" when autocomplete gets triggered, no suggestion is selected
" Use popup instead of preview window
set completeopt+=menuone,noselect,popup completeopt-=preview
set complete=.,w,b,u

""" Section: Command line settings
" on first wildchar press (<Tab>), show all matches and complete the longest common substring among them.
" on subsequent wildchar presses, cycle through matches
set wildmenu wildmode=longest:full,full
set cmdheight=2

" automatically go to the end of pasted text
vnoremap <silent> p p`]
nnoremap <silent> p p`]

" select the text that was just pasted
noremap gV `[v`]

" vp doesn't replace paste buffer
function! RestoreRegister()
  let @" = s:restore_reg
  return ''
endfunction
function! s:Repl()
  let s:restore_reg = @"
  return "p@=RestoreRegister()\<cr>"
endfunction
vmap <silent> <expr> p <sid>Repl()

" suspend vim and start a new shell
nnoremap <C-z> <Cmd>suspend<CR>
inoremap <C-z> <Cmd>suspend<CR>
xnoremap <C-z> <Cmd>suspend<CR>

""" Section: System Mappings
" Map the output of these key combinations to their actual names
imap OB <Down>
imap OA <Up>

""" Section: <S-k> settings
let s:failure_indicators = {
      \ 'python': 'no python documentation found',
      \ 'vim': 'Sorry, no help',
      \ 'sh': 'No manual entry',
      \ }
function! ChainedKeywordprg()
  let l:cword = expand("<cword>")

  if !empty(&keywordprg)
    let l:failure_indicator = get(s:failure_indicators, &ft, '')

    if &keywordprg[0] == ':'
      let l:result = execute('silent! ' . &keywordprg[1:] . ' ' . l:cword)
      if empty(l:failure_indicator) || l:result !~? l:failure_indicator
        return
      endif
    else
      let l:result = system(&keywordprg . ' ' . l:cword)
      if empty(l:failure_indicator) || l:result !~? l:failure_indicator
        execute '!' . &keywordprg . ' ' . l:cword
        return
      endif
    endif
  endif

  if exists(':LspHover')
      exe "LspHover"
  endif
endfunction
nnoremap K <Cmd>call ChainedKeywordprg()<CR>

""" Section: Line folding / splitting
" Prevents inserting two spaces after punctuation on a join (J)
set nojoinspaces
" Delete comment character when joining commented lines
set formatoptions+=j

""" Section: Motions / Text Objects
set matchpairs+=<:>
" Go to start/end of text object
function! GoStart(type) abort
  normal! `[
endfunction
function! GoEnd(type) abort
  normal! `]
endfunction
nnoremap <silent> gb <Cmd>set opfunc=GoStart<CR>g@
nnoremap <silent> ge <Cmd>set opfunc=GoEnd<CR>g@
" move ten lines at a time by holding ctrl and a directional key
noremap <C-j> 10j
noremap <C-k> 10k
nnoremap Y yg_

nnoremap } <Cmd>keepjumps normal! }<CR>
nnoremap { <Cmd>keepjumps normal! {<CR>

""" Section: Search
"""" Misc.
" While typing the search query, highlight where the first match would be.
set incsearch
" searching is only case sensitive when the query contains an uppercase letter
set ignorecase smartcase
" show match position in command window, don't show 'Search hit BOTTOM/TOP'
set shortmess-=S shortmess+=s
" Use ripgrep as the grep program, if it's available. Otherwise use the internal
" grep implementation since it's cross-platform
let &grepprg = executable('rg') ? 'rg --vimgrep --smart-case --follow' : 'internal'
" toggle search highlighting
nnoremap <silent> <Leader>\ <Cmd>set hlsearch!<CR>

"""" Use '/' and '?' search in visual mode
vnoremap <silent> * <Cmd>
  \let old_reg=getreg('y')<Bar>let old_regtype=getregtype('y')<CR>
  \gv"yy/<C-R>=&ic?'\c':'\C'<CR><C-R><C-R>=substitute(
  \escape(@y, '/\.*$^~['), '\_s\+', '\\_s\\+', 'g')<CR><CR>
  \gVzv:call setreg('y', old_reg, old_regtype)<CR>
vnoremap <silent> # <Cmd>
  \let old_reg=getreg('y')<Bar>let old_regtype=getregtype('y')<CR>
  \gv"yy?<C-R>=&ic?'\c':'\C'<CR><C-R><C-R>=substitute(
  \escape(@y, '?\.*$^~['), '\_s\+', '\\_s\\+', 'g')<CR><CR>
  \gVzv:call setreg('y', old_reg, old_regtype)<CR>

""" Section: Restore Settings
augroup SaveAndRestoreSettings
  autocmd!
  " Restore session after vim starts. The 'nested' keyword tells vim to fire events
  " normally while this autocmd is executing. By default, no events are fired
  " during the execution of an autocmd to prevent infinite loops.
  let s:session_dir = $VIMHOME . 'sessions/'
  function! RestoreOrCreateSession()
    " We omit the first element in the list since that will always be the path
    " to the vim binary e.g. /usr/local/bin/vim
    if v:argv[1:]->empty()
      call mkdir(s:session_dir, "p")
      let s:session_name =  substitute($PWD, '/', '%', 'g') . '%vim'
      let s:session_full_path = s:session_dir . s:session_name
      let s:session_cmd = filereadable(s:session_full_path) ? "source " : "mksession! "
      execute s:session_cmd . fnameescape(s:session_full_path)
    endif
  endfunction
  autocmd VimEnter * nested call RestoreOrCreateSession()
  " save session before vim exits
  function! SaveSession()
    if !empty(v:this_session)
      execute 'mksession! ' . fnameescape(v:this_session)
    endif
  endfunction
  autocmd VimLeavePre * call SaveSession()
augroup END

""" Section: Aesthetics
"""" Misc.
set linebreak
set number relativenumber
set cursorline cursorlineopt=number,line
set laststatus=2
set showtabline=1
set wrap
set listchars=tab:Â¬-,space:Â· " chars to represent tabs and spaces when 'setlist' is enabled
set signcolumn=yes " always show the sign column
let &fillchars = "foldopen: ,fold: ,vert:\u2502,stl:\ ,stlnc:\ "

" Statusline
function! MyStatusLine()
  if g:actual_curwin == win_getid()
    let l:highlight = 'StatusLine'
  else
    let l:highlight = 'StatusLineNC'
  endif
  let l:highlight_text = l:highlight . 'Text'
  let l:highlight = '%#' . l:highlight . '#'
  let l:highlight_text = '%#' . l:highlight_text . '#'
  let l:highlight_right_text = '%#StatusLineRightText#'

  if &ft ==# 'help'
    let l:special_statusline = 'Help â¯ %t'
  elseif &ft ==# 'vim-plug'
    let l:special_statusline = 'Vim Plug'
  elseif g:actual_curwin != win_getid()
    let l:special_statusline = '%t'
  elseif exists('b:NERDTree')
    let l:special_statusline = '%t'
  endif
  if exists('l:special_statusline')
    let l:special_statusline = ' ' . l:special_statusline . ' '
    return l:highlight_text . l:special_statusline . l:highlight
  endif

  let l:ale_count = ale#statusline#Count(bufnr('%'))
  let l:error_count = l:ale_count.error
  let l:warning_count = l:ale_count.warning
  let l:error = (l:error_count > 0) ? '%#StatusLineRightText# â® %#StatusLineErrorText#' . l:error_count . ' ' . (l:error_count > 1 ? 'errors' : 'error') : ''
  let l:warning = (l:warning_count > 0) ? '%#StatusLineRightText# â® %#StatusLineWarningText#' . l:warning_count . ' ' . (l:warning_count > 1 ? 'warnings' : 'warning') : ''

  return l:highlight_text . (exists('l:special_statusline') ? l:special_statusline : ' %y â¯ %h%w%q%t%m%r ') . l:highlight . '%=' . l:highlight_right_text . ' Ln %l/%L â® Col %c/%{execute("echon col(\"$\") - 1")}' . l:warning . l:error . ' '
endfunction
set statusline=%{%MyStatusLine()%}

"""" Block cursor in normal mode, thin line in insert mode, and underline in replace mode
let &t_SI.="\e[5 q" "SI = INSERT mode
let &t_SR.="\e[3 q" "SR = REPLACE mode
let &t_EI.="\e[1 q" "EI = NORMAL mode (ELSE)
function! RestoreCursor()
  " set cursor back to block
  silent execute "!echo -ne '\e[1 q'"
endfunction
function! ResetCursor()
  " reset terminal cursor to blinking bar
  silent execute "!echo -ne '\e[5 q'"
endfunction
augroup Cursor
  autocmd!
  autocmd VimLeave * call ResetCursor()
  autocmd VimSuspend * call ResetCursor()
  autocmd VimResume * call RestoreCursor()
augroup END

" Tabline
function! Tabline()
  let tabline = ''

  for i in range(tabpagenr('$'))
    let tab = i + 1
    let winnr = tabpagewinnr(tab)
    let buflist = tabpagebuflist(tab)
    let bufnr = buflist[winnr - 1]

    let bufname = bufname(bufnr)
    if bufname != ''
      let bufname = fnamemodify(bufname, ':t')
    else
      let bufname = '[No Name]'
    endif

    let tabline .= '%' . tab . 'T'
    let tabline .= (tab == tabpagenr() ? '%#TabLineSel#' : '%#TabLine#')
    let tabline .= ' %' . tab . 'Xâœ•%X '
    let tabline .= empty(gettabvar(tab, 'fzf_active')) ? bufname : 'fzf'
    let tabline .= ' '
  endfor

  let tabline .= '%T%#TabLineFill#'

  return tabline
endfunction
set tabline=%!Tabline()

" Write to file with sudo. For when I forget to use sudoedit.
" tee streams its input to stdout as well as the specified file so I suppress the output
command! SudoWrite w !sudo tee % >/dev/null

""" Section: Plugins
"""" Plugin Manager Settings
" Install vim-plug if not found
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
  \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
endif
" Install plugins if not found
function! InstallMissingPlugins()
  let l:has_missing_plugins = len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
  if l:has_missing_plugins
    PlugInstall --sync
  endif
endfunction
autocmd VimEnter * nested call InstallMissingPlugins()

"""" Start Plugin Manager
call plug#begin('~/.vim/plugged')

"""" General
" Syntax plugins for practically any language
Plug 'sheerun/vim-polyglot'
" Motions for levels of indentation
Plug 'jeetsukumaran/vim-indentwise'
  map [<Tab> <Plug>(IndentWiseBlockScopeBoundaryBegin)
  map ]<Tab> <Plug>(IndentWiseBlockScopeBoundaryEnd)
" replacement for matchit since matchit wasn't working for me
Plug 'andymass/vim-matchup'
  " Don't display offscreen matches in my statusline or a popup window
  let g:matchup_matchparen_offscreen = {}
" Additional text objects and motions
Plug 'wellle/targets.vim'
Plug 'junegunn/vim-peekaboo'
  let g:peekaboo_delay = 500 " measured in milliseconds
Plug 'inkarkat/vim-CursorLineCurrentWindow'
Plug 'farmergreg/vim-lastplace'
Plug 'tmux-plugins/vim-tmux'
  " This plugin sets a mapping for 'K' that looks up the current word in the tmux manpage.
  " Instead of a mapping, the autocmd below sets the keywordprg. This way it works with
  " my ChainedKeywordprg(). Plus this seems like the more idiomatic way to do it.
  augroup VimTmux
    autocmd FileType tmux setlocal keywordprg=:call\ tmux#man()
  augroup END
Plug 'tweekmonster/startuptime.vim'
" Determine the filetype based on the interpreter specified in a shebang
Plug 'vitalk/vim-shebang'
Plug 'bkad/CamelCaseMotion'
  let g:camelcasemotion_key = ','

"""" Autocomplete
Plug 'prabirshrestha/asyncomplete.vim'
  let g:asyncomplete_auto_completeopt = 0
  let g:asyncomplete_auto_popup = 1
  let g:asyncomplete_min_chars = 1
  let g:asyncomplete_matchfuzzy = 0
  inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"
  function! s:check_back_space() abort
    let col = col('.') - 1
    return !col || getline('.')[col - 1]  =~ '\s'
  endfunction
  inoremap <silent><expr> <TAB>
    \ pumvisible() ? "\<C-n>" :
    \ <SID>check_back_space() ? "\<TAB>" :
    \ asyncomplete#force_refresh()
  imap <C-a> <Plug>(asyncomplete_force_refresh)
  Plug 'prabirshrestha/asyncomplete-buffer.vim'
    let g:asyncomplete_buffer_clear_cache = 1
    autocmd User asyncomplete_setup
      \ call asyncomplete#register_source(asyncomplete#sources#buffer#get_source_options({
      \ 'name': 'buffer',
      \ 'allowlist': ['*'],
      \ 'events': ['InsertLeave','BufWinEnter','BufWritePost'],
      \ 'completor': function('asyncomplete#sources#buffer#completor'),
      \ }))
  Plug 'prabirshrestha/asyncomplete-file.vim'
    autocmd User asyncomplete_setup
        \ call asyncomplete#register_source(asyncomplete#sources#file#get_source_options({
        \ 'name': 'file',
        \ 'allowlist': ['*'],
        \ 'priority': 10,
        \ 'completor': function('asyncomplete#sources#file#completor')
        \ }))
  Plug 'yami-beta/asyncomplete-omni.vim'
    autocmd User asyncomplete_setup
        \ call asyncomplete#register_source(asyncomplete#sources#omni#get_source_options({
        \ 'name': 'omni',
        \ 'allowlist': ['*'],
        \ 'completor': function('asyncomplete#sources#omni#completor'),
        \ 'config': {
        \   'show_source_kind': 1,
        \ },
        \ }))
  Plug 'Shougo/neco-vim'
    Plug 'prabirshrestha/asyncomplete-necovim.vim'
      autocmd User asyncomplete_setup
          \ call asyncomplete#register_source(asyncomplete#sources#necovim#get_source_options({
          \ 'name': 'necovim',
          \ 'allowlist': ['vim'],
          \ 'completor': function('asyncomplete#sources#necovim#completor'),
          \ }))
  Plug 'prabirshrestha/async.vim'
    Plug 'wellle/tmux-complete.vim'
  " Language Server Protocol client that provides IDE like features
  " e.g. autocomplete, autoimport, smart renaming, go to definition, etc.
  Plug 'prabirshrestha/vim-lsp'
    " for debugging
    let g:lsp_log_file = $VIMHOME . 'vim-lsp-log'
    let g:lsp_fold_enabled = 0
    let g:lsp_document_code_action_signs_enabled = 0
    let g:lsp_document_highlight_enabled = 0
    " An easy way to install/manage language servers for vim-lsp.
    Plug 'mattn/vim-lsp-settings'
      " where the language servers are stored
      let g:lsp_settings_servers_dir = $VIMHOME . "vim-lsp-servers"
      call mkdir(g:lsp_settings_servers_dir, "p")

" Asynchronous linting
Plug 'dense-analysis/ale'
  " If a linter is not found don't continue to check on subsequent linting operations.
  let g:ale_cache_executable_check_failures = 1
  " Don't show popup when the mouse if over a symbol, vim-lsp
  " should be responsible for that.
  let g:ale_set_balloons = 0
  " Don't show variable information in the status line,
  " vim-lsp should be responsible for that.
  let g:ale_hover_cursor = 0
  " Only display diagnostics with a warning level or above
  " i.e. warning,error
  let g:ale_lsp_show_message_severity = "warning"
  let g:ale_lint_on_enter = 1 " lint when a buffer opens
  let g:ale_lint_on_text_changed = "always"
  let g:ale_lint_delay = 1000
  let g:ale_lint_on_insert_leave = 0
  let g:ale_lint_on_filetype_changed = 0
  let g:ale_lint_on_save = 0

"""" Coordination between plugins
""""" delimitmate, vim-endwise
" Combine enter key (<CR>) mappings from the plugins above.
" Also, if the popupmenu is visible, but no items are selected, close the
" popup and insert a newline.
imap <expr> <CR>
  \ pumvisible() ?
    \ (complete_info().selected == -1 ? '<C-y><CR>' : '<C-y>') :
    \ delimitMate#WithinEmptyPair() ?
      \ "\<C-R>=delimitMate#ExpandReturn()\<CR>" :
      \ "\<CR>\<Plug>DiscretionaryEnd"
""""" vim-lsp, ale
" A bridge between vim-lsp and ale. This works by
" sending diagnostics (e.g. errors, warning) from vim-lsp to ale.
" This way, vim-lsp will only provide LSP features
" and ALE will only provide realtime diagnostics.
Plug 'rhysd/vim-lsp-ale'
  " Only report diagnostics with a level of 'warning' or above
  " i.e. warning,error
  let g:lsp_ale_diagnostics_severity = "warning"

"""" Editing
" Automatically add closing keywords (e.g. function/endfunction in vimscript)
Plug 'tpope/vim-endwise'
  let g:endwise_no_mappings = 1
  " this way endwise triggers on 'o'
  nmap o A<CR>
" Automatically close html tags
Plug 'alvan/vim-closetag'
" Automatically insert closing braces/quotes
Plug 'Raimondi/delimitMate'
  " Given the following line (where | represents the cursor):
  "   function foo(bar) {|}
  " Pressing enter will result in:
  " function foo(bar) {
  "   |
  " }
  let g:delimitMate_expand_cr = 0
" Makes it easier to manipulate brace/bracket/quote pairs by providing commands to do common
" operations like change pair, remove pair, etc.
Plug 'tpope/vim-surround'
" For swapping two pieces of text
Plug 'tommcdo/vim-exchange'
" I use it for more robust substitutions, but it does alot more
Plug 'tpope/vim-abolish'
" Expands Emmet abbreviations to write HTML more quickly
Plug 'mattn/emmet-vim'
  let g:user_emmet_expandabbr_key = '<Leader>e'
  let g:user_emmet_mode='n'
Plug 'airblade/vim-matchquote'
Plug 'tpope/vim-commentary'

" Seamless movement between vim windows and tmux panes.
Plug 'christoomey/vim-tmux-navigator'
  let g:tmux_navigator_no_mappings = 1
  execute "set <M-h>=\eh"
  execute "set <M-j>=\ej"
  execute "set <M-k>=\ek"
  execute "set <M-l>=\el"
  noremap <silent> <M-h> :TmuxNavigateLeft<cr>
  noremap <silent> <M-l> :TmuxNavigateRight<cr>
  noremap <silent> <M-j> :TmuxNavigateDown<cr>
  noremap <silent> <M-k> :TmuxNavigateUp<cr>

" Add icons to the gutter to signify version control changes (e.g. new lines, modified lines, etc.)
Plug 'mhinz/vim-signify'
  nnoremap <Leader>vk <Cmd>SignifyHunkDiff<CR>

" fzf integration
"
" TODO: Until vim gets support for dim text in its terminals, it will be hard to see which item is
" active.
" issue: https://github.com/vim/vim/issues/8269
Plug 'junegunn/fzf'
  let g:fzf_layout = { 'window': 'tabnew' }
  augroup Fzf
    autocmd!
    " Hide statusline when fzf is active
    autocmd  FileType fzf set laststatus=0 noshowmode noruler | let t:fzf_active = 1
      \| autocmd BufLeave <buffer> set laststatus=2 showmode
    " In terminals you have to press <C-\> twice to send it to the terminal.
    " This mapping makes it so that I only have to press it once.
    " This way, I can use a <C-\> keybind in fzf more easily.
    autocmd  FileType fzf tnoremap <buffer> <C-\> <C-\><C-\>
  augroup END
  " Collection of fzf-based commands
  Plug 'junegunn/fzf.vim'
    nnoremap <silent> <Leader>m <Cmd>Marks<CR>
    nnoremap <silent> <Leader>h <Cmd>History:<CR>
    nnoremap <silent> <Leader>/ <Cmd>Commands<CR>
    nnoremap <silent> <Leader>b <Cmd>Buffers<CR>
    " recursive grep
    function! RipgrepFzf(query, fullscreen)
      let command_fmt = 'rg --hidden --column --line-number --no-heading --color=always --smart-case -- %s || true'
      let initial_command = printf(command_fmt, shellescape(a:query))
      let reload_command = printf(command_fmt, '{q}')
      let spec = {'options': ['--phony', '--query', a:query, '--bind', 'change:first+reload:'.reload_command, '--prompt', 'lines: ', '--preview', 'bat --paging=never --terminal-width (math $FZF_PREVIEW_COLUMNS - 2) {1} --highlight-line {2} | tail -n +2 | head -n -1']}
      call fzf#vim#grep(initial_command, 1, spec, a:fullscreen)
    endfunction
    command! -nargs=* -bang FindLine call RipgrepFzf(<q-args>, <bang>0)
    nnoremap <Leader>g <Cmd>FindLine<CR>
    " recursive file search
    command! -bang -nargs=* FindFile call
        \ fzf#run(fzf#wrap({
        \ 'source': 'fd --hidden --strip-cwd-prefix --type file --type symlink | tr -d "\017"',
        \ 'sink': 'edit',
        \ 'options': '--ansi --preview "bat --style=numbers,grid --paging=never --terminal-width (math \$FZF_PREVIEW_COLUMNS - 2) {} | tail -n +2 | head -n -1" --prompt="' . substitute(getcwd(), $HOME, '~', "") .'/" --keep-right'}))
    nnoremap <Leader>f <Cmd>FindFile<CR>

"""" Colors
" Opens the OS color picker and inserts the chosen color into the buffer.
Plug 'KabbAmine/vCoolor.vim'
  " Make an alias for the 'VCoolor' command with a name that is easier to remember
  command! ColorPicker VCoolor

"""" File explorer
Plug 'preservim/nerdtree', {'on': 'NERDTreeFind'}
  function! NerdTreeToggle()
    " NERDTree is open so close it.
    if exists('g:NERDTree') && g:NERDTree.IsOpen()
      silent execute 'NERDTreeToggle'
      return
    endif

    " If NERDTree can't find the current file, it prints an error and doesn't open NERDTree.
    " In which case, I call 'NERDTree' which opens NERDTree to the current directory.
    silent execute 'NERDTreeFind'
    if !g:NERDTree.IsOpen()
      silent execute 'NERDTree'
    endif
  endfunction
  execute "set <M-e>=\ee"
  nnoremap <silent> <M-e> <Cmd>call NerdTreeToggle()<CR>
  augroup NerdTree
    autocmd!
    " open/close directories with 'h' and 'l'
    autocmd FileType nerdtree nmap <buffer> l o
    autocmd FileType nerdtree nmap <buffer> h o
  augroup END
  let g:NERDTreeMouseMode = 2
  let g:NERDTreeShowHidden = 1
  let g:NERDTreeStatusline = -1
  let g:NERDTreeWinPos = "right"

"""" Colorscheme
Plug 'arcticicestudio/nord-vim'
  " Overrides
  augroup NordColorschemeOverrides
    autocmd!
    " MatchParen
    autocmd ColorScheme nord highlight MatchParen ctermfg=blue cterm=underline ctermbg=NONE
    " Transparent SignColumn
    autocmd ColorScheme nord highlight clear SignColumn
    " Transparent vertical split
    autocmd ColorScheme nord highlight VertSplit ctermbg=NONE ctermfg=8
    " statusline colors
    autocmd ColorScheme nord highlight StatusLine ctermbg=8
    autocmd ColorScheme nord highlight StatusLineNC ctermbg=8
    autocmd ColorScheme nord highlight StatusLineText ctermfg=14 ctermbg=NONE cterm=reverse
    autocmd ColorScheme nord highlight StatusLineRightText ctermfg=14 ctermbg=8
    autocmd ColorScheme nord highlight StatusLineErrorText ctermfg=1 ctermbg=8
    autocmd ColorScheme nord highlight StatusLineWarningText ctermfg=3 ctermbg=8
    autocmd ColorScheme nord highlight StatusLineNCText ctermfg=0 ctermbg=NONE cterm=reverse
    autocmd ColorScheme nord highlight StatusLineStandout ctermfg=0 ctermbg=NONE
    autocmd ColorScheme nord highlight StatusLineStandoutText ctermfg=DarkYellow ctermbg=NONE cterm=reverse
    " autocomplete popupmenu
    autocmd ColorScheme nord highlight PmenuSel ctermfg=14 ctermbg=NONE cterm=reverse
    autocmd ColorScheme nord highlight Pmenu ctermfg=black ctermbg=NONE cterm=reverse
    autocmd ColorScheme nord highlight CursorLine ctermfg=NONE ctermbg=NONE cterm=underline
    " transparent background
    autocmd ColorScheme nord highlight Normal ctermbg=NONE
    autocmd ColorScheme nord highlight NonText ctermbg=NONE
    autocmd ColorScheme nord highlight! link EndOfBuffer NonText
    " relative line numbers
    autocmd ColorScheme nord highlight LineNr ctermfg=NONE
    autocmd ColorScheme nord highlight LineNrAbove ctermfg=0
    autocmd ColorScheme nord highlight! link LineNrBelow LineNrAbove
    autocmd ColorScheme nord highlight WordUnderCursor cterm=bold
    autocmd ColorScheme nord highlight IncSearch ctermbg=8 cterm=NONE
    autocmd ColorScheme nord highlight TabLine ctermbg=NONE ctermfg=black cterm=reverse
    autocmd ColorScheme nord highlight TabLineSel ctermbg=NONE ctermfg=14 cterm=reverse
    autocmd ColorScheme nord highlight TabLineFill ctermbg=8
    autocmd ColorScheme nord highlight WildMenu ctermfg=14 ctermbg=NONE cterm=underline
    autocmd ColorScheme nord highlight Comment ctermfg=Black ctermbg=NONE
    " This variable contains a list of 16 colors that should be used as the color palette for terminals opened in vim.
    " By unsetting this, I ensure that terminals opened in vim will use the colors from the color palette of the
    " terminal emulator in which vim is running
    autocmd ColorScheme nord unlet g:terminal_ansi_colors
    " Have vim only use the colors from the 16 color palette of the terminal emulator in which it runs
    autocmd ColorScheme nord set t_Co=16
    autocmd ColorScheme nord highlight Visual ctermbg=8
    " Search hit
    autocmd ColorScheme nord highlight Search ctermfg=DarkYellow ctermbg=NONE cterm=reverse
  augroup END

"""" End Plugin Manager
call plug#end()

" Must be after plugins are loaded since this colorscheme comes from a plugin
colorscheme nord
